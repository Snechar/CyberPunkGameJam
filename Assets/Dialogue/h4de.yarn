title: H4DEs
---
<<reset>>
<<loadBG "Bar/BarBGOfficial">>

<<volumeBG .1>>
<<crossfadeBGTo "Dead Rain" 2>>

<<if !$hasStartedBefore >>
    <<fadeIn 2>>
    // X_level is the conversation level you've completed
    // X_score is whether or not you've done well with your deliveries
    //   2+ days early = +2
    //   1 day early = +1
    //   on time = 0
    //   <= 2 days late = -1
    //   > 2 days late = -2
    // X_love is how much they like you
    << set $eli_level = 0 >>
    << set $eli_score = 0 >>
    << set $eli_love = 0 >>

    << set $rose_level = 0 >>
    << set $rose_score = 0 >>

    << set $sif_level = 0 >>
    << set $sif_score = 0 >>

    << set $has_triggered_end = false >>

    <<loadLeft "Bar/Characters/Eddy">>
    <<jump FirstStart>>
<<else>>
    <<fadeIn 1>>

    << if !$has_triggered_end and $eli_level == 4 and $rose_level == 4 and $sif_level == 4 >>
        << set $has_triggered_end = true >>
        Eddy: Hey choom!
        Carrier: What's up Eddy?
        Eddy: Churn's here. Did Tychus give you the rundown?
        Carrier: Not really, just a heads up that I should keep scanning.
        Eddy: Probably thought you knew. You wondering why your clients are gone?
        -> Actually yea...
        -> Figured they'd just moved on...
            Eddy: All at once?
        Eddy: So the way it goes is this. Delta City's been winding itself up for years. Sometimes something big'll happen and relieve a bit of that tension.
        Eddy: But if it don't then the pressure just builds until the whole place is a prox mine -- waiting for anything to move wrong then *boom* things change.
        Eddy: You don't want to get caught in it. And the best thing you can do when it is here is to lay low.
        Carrier: And you think that's coming soon?
        Eddy: I mean there ain't no schedule but I've been here a long time and signs say something is about to go off.
        Eddy: First hint was when Eli started to get real quiet about the revolution and then paid his tab and said goodbye. Felt permanant.
        Carrier: Nova. Thanks.
        Eddy: Sure thing. You're welcome to stay around and hang out with me and H3X but there's probably a dry spell on new work.
        NARRATOR: That is to say "Thanks for playing, we ran out of content for this jam game."
        NARRATOR: Team Adhoc is made up of...
        ART: allstarchicken + SketchingLujan
        DEVELOPERS: envy + snechar
        NARRATIVE: swordslinger + darrow_mm + zera
        MUSIC: Junpei!
        NARRATOR: Thank you from all of us.
    << endif >>

    <<jump Arrival>>
<<endif>>
===

title: FirstStart
---
Eddy: Hey there, welcome to H4-DEs...
Eddy: Oh, Carrier, glad you're back! looking for new clients?
-> Sure, if it's not too much trouble.
   Eddy laughs
   Eddy: You're never trouble. How about you start off with a couple of tomatoes for me and we'll have a Bloody Mary next time you're in.
-> Not right now, trying to lay low.
   Eddy: I get it. What about a milkrun for me?
   You pause. You really are trying to avoid heat but Eddy has always been kind and H4-DEs is your base at the moment.
   Carrier: You know I couldn't turn you down even if I wanted to. What do you need?
   Eddy: A couple of tomatoes? Been a while since we had a real Bloody Mary around here.
Carrier: Sure thing.
<<addRequest eddy "2:tom" 1000>>
Eddy: Nova. Oh by they way, got some new seeds for you. Grow a bit.

// <<dumpRequestBook>>

<<set $isFirstStart = false>>
<<set $hasStartedBefore = true>>
<<jump Arrival>>
===

title: Arrival
---
<< unloadAll >>
<< if completedRequests("eddy") == 0 >>
    << if canFill("eddy") >>
        <<jump EddyDelivery>>
    << else >>
        Better not keep Eddy waiting, I should head to the grow house.
        <<jump ToTycoon>>
    << endif >>
<< elseif hasDelivery() >>
    What next?
    -> Chat With Eddy
        <<jump EddyChatter>>
    -> Chat with H3x
        <<jump H3xChatter>>
    -> Make deliveries.
        <<jump MakeDelivery>>
    -> Head to the grow house.
        Carrier: I'm out.
        Eddy: Nova.
<<else>>
    What next?
    -> Chat with your clients. <<if !$has_triggered_end >>
        -> Eli
            <<jump EliChatter>>
        -> Caitlyn
            <<jump RoseChatter>>
        -> Tychus & Sif
            <<jump SifChatter>>
    -> Chat With Eddy
        <<jump EddyChatter>>
    -> Chat with H3x
        <<jump H3xChatter>>
    -> Head to the grow house.
        Carrier: I'm out.
        <<jump ToTycoon>>
<<endif>>
===

title: MakeDelivery
---
<<unloadLeft>>

You've got some goods to deliver
<< if canFill("eddy") >>
// only one time we can fill eddy and he'll be the only option
-> Go see Eddy
    << jump EddyDelivery >>

// painfully list out all possible combinations of pending deliveries
<< elseif canFill("eli") and canFill("rose") and canFill("sif") >>
-> Go See Eli.
    << jump EliDelivery >>
-> Go see Caitlyn.
    << jump RoseDelivery >>
-> Go see Tychus & Sif
    << jump SifDelivery >>
<< elseif canFill("eli") and canFill("rose") >>
-> Go See Eli.
    << jump EliDelivery >>
-> Go see Caitlyn.
    << jump RoseDelivery >>
<< elseif canFill("eli") and canFill("sif") >>
-> Go See Eli.
    << jump EliDelivery >>
-> Go see Tychus & Sif
    << jump SifDelivery >>
<< elseif canFill("rose") and canFill("sif") >>
-> Go see Caitlyn.
    << jump RoseDelivery >>
-> Go see Tychus & Sif
    << jump SifDelivery >>
<< elseif canFill("eli") >>
-> Go See Eli.
    << jump EliDelivery >>
<< elseif canFill("rose") >>
-> Go see Caitlyn.
    << jump RoseDelivery >>
<< elseif canFill("sif") >>
-> Go see Tychus & Sif
    << jump SifDelivery >>
<< else >>
    On the other hand you don't have anything ready for delivery right now.
    << jump H4DEs >>
<< endif >>
===

title: FindWork
---

<< if workAvailable("eli") and workAvailable("rose") and workAvailable("sif") >>
-> Eli .
    << jump EliDelivery >>
-> Go see Caitlyn.
    << jump RoseDelivery >>
-> Go see Tychus & Sif
    << jump SifDelivery >>
<< elseif workAvailable("eli") and workAvailable("rose") >>
-> Go See Eli.
    << jump EliDelivery >>
-> Go see Caitlyn.
    << jump RoseDelivery >>
<< elseif workAvailable("eli") and workAvailable("sif") >>
-> Go See Eli.
    << jump EliDelivery >>
-> Go see Tychus & Sif
    << jump SifDelivery >>
<< elseif workAvailable("rose") and workAvailable("sif") >>
-> Go see Caitlyn.
    << jump RoseDelivery >>
-> Go see Tychus & Sif
    << jump SifDelivery >>
<< elseif workAvailable("eli") >>
-> Go See Eli.
    << jump EliDelivery >>
<< elseif workAvailable("rose") >>
-> Go see Caitlyn.
    << jump RoseDelivery >>
<< elseif workAvailable("sif") >>
-> Go see Tychus & Sif
    << jump SifDelivery >>
<< else >>
    On the other hand you don't have anything ready for delivery right now.
    << jump H4DEs >>
<< endif >>

TODO: how do we even decide who's available for requests.
<<jump Arrival>>
===

title: ToTycoon
---
<<crossfadeBGTo "neon harvest" 3>>
<<fadeOut 1.5>>
<<unloadAll>>
<<switchScene "tycoon">>
===